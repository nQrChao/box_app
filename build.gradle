plugins {
    id 'com.android.application'
    id 'vmos-build'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.android'
}
if (project.hasProperty('MOD_SRC_DIR') && MOD_SRC_DIR.toLowerCase().contains("huawei")) {
    println "\n\033[40;36;4m--- 检测到华为渠道，应用 AppGallery Connect 插件 ---\033[0m"
    apply plugin: 'com.huawei.agconnect'
}
def keyPropertiesFile = file("../mod_common/keystore.properties")
def keyProperties = new Properties()
keyProperties.load(new FileInputStream(keyPropertiesFile))
ext.KEY_PROPERTIES = keyProperties

def static buildTime() {
    return new Date().format("_yyyyMMdd.HHmm", TimeZone.getTimeZone("GMT+08:00"))
}

ext.moduleType = 'app'
//apply from: '../mod_common/base.gradle'
apply from: '../mod_common/channel_config.gradle'


android {
    namespace "com.boxapp.project"
    compileSdk 34
    flavorDimensions "channel"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
    }

    buildFeatures {
        buildConfig = true
        dataBinding = true
        viewBinding = true
    }


    compileOptions {
        targetCompatibility JavaVersion.VERSION_17
        sourceCompatibility JavaVersion.VERSION_17
    }

    signingConfigs {
        def allKeys = KEY_PROPERTIES.stringPropertyNames()
        def uniqueSigningNames = allKeys.collect { it.split('_')[0] }.toSet()
        // 3. 遍历这些 *唯一* 的名称，为每个名称创建 *一个* 签名配置
        uniqueSigningNames.each { configName ->
            if (configName) {
                create(configName) {
                    storeFile file(KEY_PROPERTIES["${configName}_storeFile"])
                    keyAlias KEY_PROPERTIES["${configName}_keyAlias"]
                    keyPassword KEY_PROPERTIES["${configName}_keyPassword"]
                    storePassword KEY_PROPERTIES["${configName}_storePassword"]
                    v1SigningEnabled true
                    v2SigningEnabled true
                }
            }
        }
    }

    packagingOptions {
        // 明确告诉 Gradle 优先选择第一个遇到的同名文件
        pickFirst 'lib/arm64-v8a/libijkplayer.so'
        pickFirst 'lib/armeabi/libijkplayer.so'
        pickFirst 'lib/armeabi-v7a/libijkplayer.so'
        pickFirst 'lib/x86/libijkplayer.so'
        pickFirst 'lib/x86_64/libijkplayer.so'

        // 如果 libijkffmpeg.so 也存在冲突，同样加上
        pickFirst 'lib/arm64-v8a/libijkffmpeg.so'
        pickFirst 'lib/armeabi-v7a/libijkffmpeg.so'
        pickFirst 'lib/armeabi/libijkffmpeg.so'
        pickFirst 'lib/x86/libijkffmpeg.so'
        pickFirst 'lib/x86_64/libijkffmpeg.so'

        pickFirst 'lib/arm64-v8a/libijksdl.so'
        pickFirst 'lib/armeabi/libijksdl.so'
        pickFirst 'lib/armeabi-v7a/libijksdl.so'
        pickFirst 'lib/x86/libijksdl.so'
        pickFirst 'lib/x86_64/libijksdl.so'

        doNotStrip "*/armeabi-v7a/*.so"
        doNotStrip "*/x86/*.so"
        doNotStrip "*/arm64-v8a/*.so"
        doNotStrip "*/x86_64/*.so"
        doNotStrip "armeabi.so"

    }

    applicationVariants.configureEach { variant ->
        def currentConfig = channelConfigsData.get(variant.flavorName)
        if (currentConfig == null) {
            println "警告: 找不到 Flavor [${variant.flavorName}] 对应的渠道配置，已跳过。"
            return
        }
        def finalConfig = defaultConfigData + currentConfig
        def modId = finalConfig.modId.replace("\"", "")
        def appName = finalConfig.appName.replace("\"", "")
        def channelName = finalConfig.oChannelName.replace("\"", "")
        variant.outputs.configureEach { output ->
            outputFileName = appName + '_'
            outputFileName += channelName + '_'
            outputFileName += variant.applicationId + '_'
            outputFileName += modId + "_"
            outputFileName += variant.buildType.name + "_v"
            outputFileName += variant.versionCode + "_"
            outputFileName += variant.versionName.concat(buildTime())
            outputFileName += '.apk'
        }
    }

    buildTypes {
        debug {
        }

        release {
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/activity',
                            'src/main/res/layouts/aop',
                            'src/main/res/layouts/fragment',
                            'src/main/res/layouts/layout',
                            'src/main/res/layouts/common',
                            'src/main/res/layouts/dialog',
                            'src/main/res/layouts/listitem',
                            'src/main/res/layouts/other',
                            'src/main/res/layouts/tab',
                            'src/main/res/layouts/audit',
                            'src/main/res/layouts/audit2',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
    }

    lintOptions {
        disable 'MissingTranslation'
        abortOnError false
        checkReleaseBuilds false
    }

    // 避免打包aapt错误。比如png格式图片错误
    aaptOptions {
        cruncherEnabled = false
    }

    repositories {
        maven {
            url = uri("libs")
        }
    }


    productFlavors {
        channelConfigsData.each { flavorName, channelConfig ->
            create(flavorName) {
                // 合并默认配置和渠道配置
                def finalConfig = defaultConfigData + channelConfig
                // 指定维度
                dimension "channel"
                // 设置应用专属配置
                applicationId finalConfig.appPackageName
                versionCode finalConfig.appVersionCode
                versionName finalConfig.appVersionName
                resValue "string", "app_name", finalConfig.appName
                resValue "string", "app_refer_name", finalConfig.appName
                // 设置签名
                if (finalConfig.signingName) {
                    signingConfig signingConfigs.getByName(finalConfig.signingName)
                }
                // 注入 BuildConfig 字段
                buildConfigField "String", "GAME_O_ID", finalConfig.oAppId
                buildConfigField "String", "GAME_O_CHANNEL_NAME", finalConfig.oChannelName
                buildConfigField "String", "APP_FILING", finalConfig.appFiling
                buildConfigField "String", "MOD_ID", finalConfig.modId
                buildConfigField "String", "MOD_NAME", finalConfig.modName
                buildConfigField "String", "MOD_VASID", finalConfig.modVasId
                buildConfigField "String", "MOD_API_VERSION", finalConfig.modApiVersion
                buildConfigField "String", "AUTH_LOGIN_SIGN_INFO", finalConfig.appAuthLoginSign
                buildConfigField "String", "TENCENT_APP_ID", finalConfig.appTencentAppId
                buildConfigField "String", "TENCENT_APP_KEY", finalConfig.appTencentAppKey
                // 注入 Manifest Placeholders
                manifestPlaceholders = [
                        'app_name'    : finalConfig.appName,
                        'app_icon'    : finalConfig.appIcon,
                        "O_APP_ID"    : finalConfig.oAppId,
                        "O_APP_KEY"   : finalConfig.oAppKey,
                        "O_APP_SECRET": finalConfig.oAppSecret,
                        APPLOG_SCHEME : "rangersapplog.byAx6uYt".toLowerCase(),
                        tencentScheme : "tencent",
                ]
            }
        }


    }


}

// 快捷命令映射
tasks.register('buildDebug') {
    dependsOn 'assembleDebug'
    group = 'build'
    description = '仅生成调试包'
}

tasks.register('buildRelease') {
    dependsOn 'assembleRelease'
    group = 'build'
    description = '仅生成正式包'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation files('../mod_common/ModAAR/cloud/asr-realtime-release.aar')

    implementation libs.google.material
    implementation libs.google.exoplayer

    //implementation libs.blankj.utilcode
    implementation libs.flycotablayout.lib

    //implementation project(':mod_common')

    channelConfigsData.each { flavorName, channelConfig ->
        def finalConfig = defaultConfigData + channelConfig
        if (finalConfig.hasVideo) { // Groovy 会自动处理 '== true'
            println "✅ 正在为 Flavor [${flavorName}] 添加视频依赖 (exoplayer)"
            add("${flavorName}Implementation", libs.google.exoplayer)
        }
    }

}


